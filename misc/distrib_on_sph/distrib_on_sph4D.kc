enum {NPT=64}
() //3DGrad
{
   static T[NPT][4];
   static n=1000;
   if(numframes==0){
      get_random_pts_on_sphere2(T);
   }
   cls(0);
   clz(1e30);
   mcam(3);
   
   setcol(0x2f8fff);
   drawTN(T);
   drawtriedra(1);
   if(n>0.000001) n=iter(T);
   printnum(n);
   for(i=0;i<NPT;i++){
      x=(0.5*T[i][0]+0.5)*255;
      y=(0.5*T[i][1]+0.5)*255;
      z=(0.5*T[i][2]+0.5)*255;
      w=(0.5*T[i][3]+0.5)*255;
      setcol(x,x,x);setpix(i,yres-4);
      setcol(y,y,y);setpix(i,yres-3);
      setcol(z,z,z);setpix(i,yres-2);
      setcol(w,w,w);setpix(i,yres-1);
   }
}
iter(T[NPT][4]){
   static F[NPT][4];
   a=1/NPT;dt=1;
   for(i=0;i<NPT;i++){
      F[i][0]=0;F[i][1]=0;F[i][2]=0;F[i][3]=0;
   }
   for(i=0;i<NPT;i++){
      for(j=i+1;j<NPT;j++){
         ps=T[i][0]*T[j][0]+T[i][1]*T[j][1]+T[i][2]*T[j][2]+T[i][3]*T[j][3];
         id=a/(1-ps);
         F[i][0]+=id*(T[i][0]*ps-T[j][0]);
         F[i][1]+=id*(T[i][1]*ps-T[j][1]);
         F[i][2]+=id*(T[i][2]*ps-T[j][2]);
         F[i][3]+=id*(T[i][3]*ps-T[j][3]);
         
         F[j][0]+=id*(T[j][0]*ps-T[i][0]);
         F[j][1]+=id*(T[j][1]*ps-T[i][1]);
         F[j][2]+=id*(T[j][2]*ps-T[i][2]);
         F[j][3]+=id*(T[j][3]*ps-T[i][3]);
      }
   }
   nm=0;
   for(i=0;i<NPT;i++){
      n=F[i][0]^2+F[i][1]^2+F[i][2]^2+F[i][3]^2;
      nm=max(nm,n);
   }
   nm=sqrt(nm);
   //dt/=nm+1;
   for(i=0;i<NPT;i++){
      T[i][0]+=dt*F[i][0];
      T[i][1]+=dt*F[i][1];
      T[i][2]+=dt*F[i][2];
      T[i][3]+=dt*F[i][3];
      is=1/sqrt(T[i][0]^2+T[i][1]^2+T[i][2]^2+T[i][3]^2);
      T[i][0]*=is;T[i][1]*=is;T[i][2]*=is;T[i][3]*=is;
   }
   //T[0][0]=1;T[0][1]=0;T[0][2]=0;T[0][3]=0;
   nm
}
get_random_pts_on_sphere2(T[NPT][4]){
   for(i=0;i<NPT;i++){
      x=nrnd;y=nrnd;z=nrnd;w=nrnd;
      ir=1/sqrt(x*x+y*y+z*z+w*w);
      T[i][0]=ir*x;T[i][1]=ir*y;T[i][2]=ir*z;T[i][3]=ir*w;
   }
}
get_random_pts_on_sphere1(T[NPT][4]){
   for(i=0;i<NPT;i++){
      t=(rnd*2-1)*PI;u=rnd*2-1;
      su=sqrt(max(0,1-u*u));
      T[i][0]=su*cos(t);T[i][1]=su*sin(t);T[i][2]=u;
      u=rnd*2-1;
      su=sqrt(max(0,1-u*u));
      T[i][0]*=su;T[i][1]*=su;T[i][2]*=su;T[i][3]=u;
   }
}
drawTN(T[NPT][4]){
   for(i=0;i<NPT;i++){
      drawsph(T[i][0],T[i][1],T[i][2],0.05);
   }
}
drawtriedra(l)
{
   setcol(255,0,0);
   moveto(0,0,0);
   lineto(l,0,0);
   setcol(0,255,0);
   moveto(0,0,0);
   lineto(0,l,0);
   setcol(0,0,255);
   moveto(0,0,0);
   lineto(0,0,l);
}
mcam(d)
{
   p=-(mousy-yres*0.5)*0.01;
   t=-pi/2+(mousx-xres*0.5)*0.01;
   fx=-cos(p)*cos(t);fy=-cos(p)*sin(t);fz=-sin(p);
   dx=cos(p-pi/2)*cos(t);dy=cos(p-pi/2)*sin(t);dz=sin(p-pi/2);
   rx=cos(t+pi/2);ry=sin(t+pi/2);rz=0;
   setcam(-d*fx,-d*fy,-d*fz,rx,ry,rz,dx,dy,dz,fx,fy,fz);
}