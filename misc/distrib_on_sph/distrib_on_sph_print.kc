enum {NPT=16}
() //3DGrad
{
   static T[NPT][3];
   static n=10000000;
   static printit=0;
   if(numframes==0){
      get_random_pts_on_sphere2(T);
      while(n>0.000001) {
         n=iter(T);
      }
   }
   cls(0);
   clz(1e30);
   mcam(3);
   
   setcol(0x2f8fff);
   drawTN(T);
   drawtriedra(1);

      for (i=0;i<NPT;i++){
         printf("\ng[%g][0]=",i);printf("%g",T[i][0]);
         printf("; g[%g][1]=",i);printf("%g",T[i][1]);
         printf("; g[%g][2]=",i);printf("%g",T[i][2]);
      }
}
iter(T[NPT][3]){
   static F[NPT][3];
   a=1/NPT;dt=1;
   for(i=0;i<NPT;i++){
      F[i][0]=0;F[i][1]=0;F[i][2]=0;
   }
   for(i=0;i<NPT;i++){
      for(j=i+1;j<NPT;j++){
         ps=T[i][0]*T[j][0]+T[i][1]*T[j][1]+T[i][2]*T[j][2];
         id=a/(1-ps);
         F[i][0]+=id*(T[i][0]*ps-T[j][0]);
         F[i][1]+=id*(T[i][1]*ps-T[j][1]);
         F[i][2]+=id*(T[i][2]*ps-T[j][2]);
         
         F[j][0]+=id*(T[j][0]*ps-T[i][0]);
         F[j][1]+=id*(T[j][1]*ps-T[i][1]);
         F[j][2]+=id*(T[j][2]*ps-T[i][2]);
      }
   }
   nm=0;
   for(i=0;i<NPT;i++){
      n=F[i][0]^2+F[i][1]^2+F[i][2]^2;
      nm=max(nm,n);
   }
   nm=sqrt(nm);
   //dt/=nm+1;
   for(i=0;i<NPT;i++){
      T[i][0]+=dt*F[i][0];
      T[i][1]+=dt*F[i][1];
      T[i][2]+=dt*F[i][2];
      is=1/sqrt(T[i][0]^2+T[i][1]^2+T[i][2]^2);
      T[i][0]*=is;T[i][1]*=is;T[i][2]*=is;
   }
   nm
}
get_random_pts_on_sphere2(T[NPT][3]){
   for(i=0;i<NPT;i++){
      x=nrnd;y=nrnd;z=nrnd;
      ir=1/sqrt(x*x+y*y+z*z);
      T[i][0]=ir*x;T[i][1]=ir*y;T[i][2]=ir*z;
   }
}
get_random_pts_on_sphere1(T[NPT][3]){
   for(i=0;i<NPT;i++){
      t=(rnd*2-1)*PI;u=rnd*2-1;
      su=sqrt(max(0,1-u*u));
      T[i][0]=su*cos(t);T[i][1]=su*sin(t);T[i][2]=u;
   }
}
drawTN(T[NPT][3]){
   for(i=0;i<NPT;i++){
      drawsph(T[i][0],T[i][1],T[i][2],0.05);
   }
}
drawtriedra(l)
{
   setcol(255,0,0);
   moveto(0,0,0);
   lineto(l,0,0);
   setcol(0,255,0);
   moveto(0,0,0);
   lineto(0,l,0);
   setcol(0,0,255);
   moveto(0,0,0);
   lineto(0,0,l);
}
mcam(d)
{
   p=-(mousy-yres*0.5)*0.01;
   t=-pi/2+(mousx-xres*0.5)*0.01;
   fx=-cos(p)*cos(t);fy=-cos(p)*sin(t);fz=-sin(p);
   dx=cos(p-pi/2)*cos(t);dy=cos(p-pi/2)*sin(t);dz=sin(p-pi/2);
   rx=cos(t+pi/2);ry=sin(t+pi/2);rz=0;
   setcam(-d*fx,-d*fy,-d*fz,rx,ry,rz,dx,dy,dz,fx,fy,fz);
}